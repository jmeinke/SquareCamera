plugins {
    id 'com.gladed.androidgitversion' version '0.3.4'
}

apply plugin: "com.android.library"

version = "1.1.0"
group = "com.github.boxme"

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildTools
    resourcePrefix "squarecamera__"

    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion targetSdk

        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    packagingOptions {
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/LICENSE.txt"
    }
}

dependencies {
    compile fileTree(dir: "libs", include: ["*.jar"])
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:support-v4:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = "sources"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
